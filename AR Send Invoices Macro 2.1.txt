Sub ListFilenames()
    Dim folderPath As String
    Dim ws As Worksheet
    Dim rowCounter As Long
    Dim fileDialog As fileDialog
    
    Call DaveCo
    
    ' Create a FileDialog object to select a folder
    Set fileDialog = Application.fileDialog(msoFileDialogFolderPicker)
    With fileDialog
        .Title = "Select a Folder"
        .AllowMultiSelect = False
        ' .InitialFileName = "C:\Users\" & Environ("UserName") & "\"
        
        If .Show = -1 Then
            folderPath = .SelectedItems(1) ' Get the selected folder path
        Else
            MsgBox "No folder selected. Exiting macro.", vbExclamation
            Exit Sub
        End If
    End With

    ' Set the worksheet
    On Error Resume Next
    Set ws = Worksheets("Get_Filenames")
    On Error GoTo 0

    ' If the worksheet doesn't exist, create it
    If ws Is Nothing Then
        Set ws = Worksheets.Add(After:=Sheets(Sheets.Count))
        ws.Name = "Get_Filenames"
    End If

    ' Add labels to columns (if not already added)
    If ws.Cells(1, 1).Value <> "Filename" Then
        ws.Cells(1, 1).Value = "Filename"
        ws.Cells(1, 2).Value = "File Path"
        ws.Cells(1, 3).Value = "Sub Folder"
        ws.Cells(1, 4).Value = "Date Modified"
    End If
    
    ' Clear existing data from row 2 to the last non-empty row
    Dim lastRow As Long
    lastRow = ws.Cells(ws.Rows.Count, 1).End(xlUp).Row
    If lastRow > 1 Then
        ws.Range("A2:Z" & lastRow).ClearContents
    End If
    
    ' Update cell I1 with the last refresh timestamp
    ws.Cells(12, 9).Value = "Last Refresh: " & Now
    
    ' Find the next empty row in column A
    rowCounter = ws.Cells(ws.Rows.Count, 1).End(xlUp).Row + 1

    ' function to list filenames
    ListFiles folderPath, ws, rowCounter

    ' Sort the data by the "Date Modified" column (column D)
    ws.Sort.SortFields.Clear
    ws.Sort.SortFields.Add Key:=ws.Range("D:D"), SortOn:=xlSortOnValues, Order:=xlDescending, DataOption:=xlSortNormal
    With ws.Sort
        .SetRange ws.UsedRange
        .Header = xlYes
        .MatchCase = False
        .Orientation = xlTopToBottom
        .SortMethod = xlPinYin
        .Apply
    End With

End Sub

Sub ListFiles(folderPath As String, ws As Worksheet, ByRef rowCounter As Long, Optional precedingPath As String = "")
    Dim fso As Object
    Dim folder As Object
    Dim subfolder As Object
    Dim file As Object
    
    

    ' Create a FileSystemObject
    Set fso = CreateObject("Scripting.FileSystemObject")

    ' Get the folder object
    Set folder = fso.GetFolder(folderPath)

    ' Loop through all files in the folder
    For Each file In folder.Files
        ' Extract the file name without the path
        Dim fileName As String
        fileName = fso.GetFileName(file.Path)

        ' Output the file name in column A, the preceding path in column B, the subfolder name in column C, and the date modified in column D
        ws.Cells(rowCounter, 1).Value = fileName
        ws.Cells(rowCounter, 2).Value = precedingPath
        ws.Cells(rowCounter, 3).Value = folder.Name
        ws.Cells(rowCounter, 4).Value = file.DateLastModified
        rowCounter = rowCounter + 1
    Next file

    ' Loop through all subfolders and call the function recursively
    For Each subfolder In folder.SubFolders
        ListFiles subfolder.Path, ws, rowCounter, folder.Path
    Next subfolder
    

    
End Sub

Sub DaveCo()
    ' Display message box for DaveCo with two options
    Dim userChoice As Integer
    userChoice = MsgBox("Your free trial to DaveCo has expired... Give Dave Pizza?", _
                        vbQuestion + vbYesNo, "DaveCo Free Trial Expired")

    ' Handle the user's choice
    If userChoice = vbYes Then
        ' If "Give Dave Pizza" is chosen (Yes button)
        MsgBox "Thank you for giving Dave pizza!", vbInformation, "Thank You"
    Else
        MsgBox "Continue Stealing", vbInformation, "Thank You"
    End If

 
End Sub

