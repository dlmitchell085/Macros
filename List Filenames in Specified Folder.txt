Sub ListFilenames()
    Dim folderPath As String
    Dim ws As Worksheet
    Dim rowCounter As Long

    ' Define the folder path
    folderPath = "C:\Users\" & Environ("UserName") & "\Ways2Well\Supply Chain - Documents\01-Receiving Documents\"

    ' Set or create the worksheet
    On Error Resume Next
    Set ws = Worksheets("Updates")
    On Error GoTo 0

    If ws Is Nothing Then
        Set ws = Worksheets.Add(After:=Sheets(Sheets.Count))
        ws.Name = "Updates"
    End If

    ' Add headers if not present
    If ws.Cells(1, 1).Value <> "Filename" Then
        ws.Cells(1, 1).Value = "Filename"
        ws.Cells(1, 2).Value = "File Path"
        ws.Cells(1, 3).Value = "Sub Folder"
        ws.Cells(1, 4).Value = "Date Modified"
    End If

    ' rest data field
    ws.Range("A2:D" & ws.Rows.Count).ClearContents

    ' Find the next empty row
    rowCounter = 2

    ' Call function to list filenames
    ListFiles folderPath, ws, rowCounter

    ' Sort the data by the "Date Modified" column (column D)
    ws.Sort.SortFields.Clear
    ws.Sort.SortFields.Add Key:=ws.Range("D:D"), SortOn:=xlSortOnValues, Order:=xlDescending, DataOption:=xlSortNormal
    With ws.Sort
        .SetRange ws.UsedRange
        .Header = xlYes
        .MatchCase = False
        .Orientation = xlTopToBottom
        .SortMethod = xlPinYin
        .Apply
    End With
End Sub

Sub ListFiles(folderPath As String, ws As Worksheet, ByRef rowCounter As Long, Optional precedingPath As String = "")
    Dim fso As Object
    Dim folder As Object
    Dim subfolder As Object
    Dim file As Object

    ' Create a FileSystemObject
    Set fso = CreateObject("Scripting.FileSystemObject")

    ' Get the folder object
    Set folder = fso.GetFolder(folderPath)

    ' Loop through all files in the folder
    For Each file In folder.Files
        ' Output the file details to the worksheet
        ws.Cells(rowCounter, 1).Value = fso.GetFileName(file.Path)
        ws.Cells(rowCounter, 2).Value = precedingPath
        ws.Cells(rowCounter, 3).Value = folder.Name
        ws.Cells(rowCounter, 4).Value = file.DateLastModified
        rowCounter = rowCounter + 1
    Next file

    ' Loop through all subfolders and call the function recursively
    For Each subfolder In folder.SubFolders
        ListFiles subfolder.Path, ws, rowCounter, folder.Path
    Next subfolder
End Sub


